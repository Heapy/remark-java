// Remark is a library for taking (X)HTML input and outputting clean Markdown, Markdown Extra, or MultiMarkdown compatible text.
setGroup("by.heap.remark")
setVersion("1.2.0-SNAPSHOT")

buildscript {
    ext {
        kotlinVersion = "1.1.2"
        junitVersion = "5.0.0-M4"
        junitPlatformVersion = "1.0.0-M4"
    }
    repositories {
        jcenter()
    }

    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
        classpath("org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion")
        classpath("com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3")
    }
}

apply plugin: "kotlin"
apply plugin: "org.junit.platform.gradle.plugin"
apply plugin: "com.jfrog.bintray"
apply plugin: "maven-publish"
apply plugin: "java-library"

repositories {
    jcenter()
}

dependencies {
    compileOnly("org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion")
    compile("org.jsoup:jsoup:1.6.3")
    compile("org.apache.commons:commons-lang3:3.4")

    // needed to use command line utility
    compileOnly("commons-cli:commons-cli:1.2")

    testCompile("commons-io:commons-io:2.4")
    testCompile("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.5"
    distributionUrl = "http://services.gradle.org/distributions/gradle-${gradleVersion}-bin.zip"
}

publishing {
    publications {
        RemarkKotlinPublication(MavenPublication) {
            from components.java
            groupId project.group
            artifactId 'remark-kotlin'
            version project.version
        }
    }
}

bintray {
    user = project.hasProperty("bintrayUser") ? project.property("bintrayUser") : System.getenv("BINTRAY_USER")
    key = project.hasProperty("bintrayApiKey") ? project.property("bintrayApiKey") : System.getenv("BINTRAY_API_KEY")
    configurations = ["archives"]
    pkg {
        userOrg = "heapy"
        repo = "heap"
        name = "remark-kotlin"
        licenses = ["Apache-2.0"]
        vcsUrl = "https://github.com/Heapy/remark-kotlin"
        labels = ["markdown", "kotlin"]
        publications = ['RemarkKotlinPublication']
        publicDownloadNumbers = true
        version {
            name = project.version
            released = new Date()
            vcsTag = "v1.2.0"
        }
    }
}
